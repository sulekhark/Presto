.include "T.dom"                         # domain of types
.include "M.dom"                         # domain of methods
.include "H.dom"                         # domain of allocation sites
.include "V.dom"                         # domain of variables
.include "F.dom"                         # domain of fields
.include "EH.dom"                        # domain of exception handlers


relH(h:H) input
relT(t:T) input
ThrowMH(m:M,h:H) input                   # The exception object h escapes Method m.
CatchVH(v:V,h:H) input                   # Variable v which is the formal parameter of some catch handler, points-to exception object h.
ExceptionType(t:T) input                 # Type t is an exception type
HT(h:H,t:T) input                        # Allocation site h as Type t
entryPtM(m:M) input                      # Entry point methods of the assembly.
WrapsEx(h2:H,h1:H) input                 # Exception object h2 wraps exception object h1
LinkMatch(m1:M,v1:V,t1:T,m2:M,v2:V,t2:T) input
MoveNextEH(m:M,eh:EH) input
VarEH(eh:EH,v:V) input

Caught(t:T,h:H) printtuples              # h is an exception object of Type t that is caught somewhere within the application
Escaping(t:T,h:H) printtuples            # h is an exception object of Type t that is escaping from the application
WEscaping(t1:T,h1:H,t2:T,h2:H) printtuples


# Rules
Escaping(t,h) :- ThrowMH(m,h), HT(h,t), ExceptionType(t), entryPtM(m).
WEscaping(t2,h2,t1,h1) :- WrapsEx(h1,h2), Escaping(t1,h1), LinkMatch(m2,v2,t2,m1,v1,t1), HT(h2,t2).
WEscaping(t3,h3,t2,h2) :- WrapsEx(h2,h3), WEscaping(t2,h2,t1,h1), LinkMatch(m3,v3,t3,m2,v2,t2), HT(h3,t3).
# Modeling Windows TPL (Task Parallel Library) behavior.
WEscaping(t2,h2,t1,h1) :- WrapsEx(h1,h2), Escaping(t1,h1), CatchVH(v2,h2), VarEH(eh2,v2), MoveNextEH(m,eh2), HT(h2,t2).
WEscaping(t3,h3,t2,h2) :- WrapsEx(h2,h3), WEscaping(t2,h2,t1,h1), CatchVH(v3,h3), VarEH(eh3,v3), MoveNextEH(m,eh3), HT(h3,t3).
Caught(t,h) :- CatchVH(v,h), HT(h,t), ExceptionType(t).

.include "relH.datalog"
.include "relT.datalog"
.include "ThrowMH.datalog"
.include "CatchVH.datalog"
.include "ExceptionType.datalog"
.include "HT.datalog"
.include "entryPtM.datalog"
.include "WrapsEx.datalog"
.include "LinkMatch.datalog"
.include "MoveNextEH.datalog"
.include "VarEH.datalog"
